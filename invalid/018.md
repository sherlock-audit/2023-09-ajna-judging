Real Pastel Starfish

high

# Incorrect implementation of `BPF` leads to kicker losing rewards in a `take` action
## Summary

When a `take` action takes place, if `ThresholdPrice >= NeutralPrice` and `PriceTake = NeutralPrice`, the Bond Payment Factor (`BPF`) should be `bondFactor` but it will be 0, causing the loss of rewards of the kicker in that action. 

## Vulnerability Detail

The Bond Payment Factor (`BPF`) is the formula that determines the reward/penalty over the bond of a kicker in each `take` action. According to the whitepaper, the formula is described as:

```solidity
// If TP < NP
BPF = bondFactor * min(1, max(-1, (NP - price) / (NP - TP)))

// If TP >= NP
BPF = bondFactor     (if price <= NP)
BPF = -bondFactor    (if price > NP)
```

The implementation of this formula is the following:
https://github.com/sherlock-audit/2023-09-ajna/blob/main/ajna-core/src/libraries/helpers/PoolHelper.sol#L382-L411
```solidity
function _bpf(
    uint256 debt_,
    uint256 collateral_,
    uint256 neutralPrice_,
    uint256 bondFactor_,
    uint256 auctionPrice_
) pure returns (int256) {
    int256 thresholdPrice = int256(Maths.wdiv(debt_, collateral_));

    int256 sign;
    if (thresholdPrice < int256(neutralPrice_)) {
        // BPF = BondFactor * min(1, max(-1, (neutralPrice - price) / (neutralPrice - thresholdPrice)))
        sign = Maths.minInt(
            1e18,
            Maths.maxInt(
                -1 * 1e18,
                PRBMathSD59x18.div(
                    int256(neutralPrice_) - int256(auctionPrice_),
                    int256(neutralPrice_) - thresholdPrice
                )
            )
        );
    } else {
        int256 val = int256(neutralPrice_) - int256(auctionPrice_);
        if (val < 0 )      sign = -1e18;
        else if (val != 0) sign = 1e18; // @audit Sign will be zero when NP = auctionPrice
    }

    return PRBMathSD59x18.mul(int256(bondFactor_), sign);
}
```

The issue is that the implementation of the `BPF` formula in the code doesn't match the specification, leading to the loss of rewards in that `take` action in cases where `TP >= NP` and `price = NP`. 

As showed in the above snippet, in cases where `TP >= NP` and `NP = price` (thus `val = 0`) the function won't set a value for `sign` (will be `0` by default) so that will result in a computed `BPF` of `0`, instead of `bondFactor` that would be the correct `BPF`.

## Impact

The kicker will lose the rewards in that `take` action if the previous conditions are satisfied. 

While the probability of this conditions to be met is not usual, the impact is the loss of rewards for that kicker and that may cause to lose part of the bond if later a `take` is performed with a negative `BPF`. 

## Code Snippet

https://github.com/sherlock-audit/2023-09-ajna/blob/main/ajna-core/src/libraries/helpers/PoolHelper.sol#L407

## Tool used

Manual Review

## Recommendation

Change the `_bpf` function to match the specification in order to fairly distribute the rewards in a `take` action:

```diff
function _bpf(
    uint256 debt_,
    uint256 collateral_,
    uint256 neutralPrice_,
    uint256 bondFactor_,
    uint256 auctionPrice_
) pure returns (int256) {
    int256 thresholdPrice = int256(Maths.wdiv(debt_, collateral_));

    int256 sign;
    if (thresholdPrice < int256(neutralPrice_)) {
        // BPF = BondFactor * min(1, max(-1, (neutralPrice - price) / (neutralPrice - thresholdPrice)))
        sign = Maths.minInt(
            1e18,
            Maths.maxInt(
                -1 * 1e18,
                PRBMathSD59x18.div(
                    int256(neutralPrice_) - int256(auctionPrice_),
                    int256(neutralPrice_) - thresholdPrice
                )
            )
        );
    } else {
        int256 val = int256(neutralPrice_) - int256(auctionPrice_);
        if (val < 0 )      sign = -1e18;
-       else if (val != 0) sign = 1e18;
+       else               sign = 1e18;
    }

    return PRBMathSD59x18.mul(int256(bondFactor_), sign);
}
```
